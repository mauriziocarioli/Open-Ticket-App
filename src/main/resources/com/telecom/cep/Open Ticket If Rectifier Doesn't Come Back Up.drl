package com.telecom.open_ticket;

import java.util.Date;
import com.telecom.Issue;
import com.telecom.cep.AlertEvent;

rule "Open Ticket If Rectifier Doesn't Come Back Up After 5 Minutes"
dialect "mvel"
no-loop
	when
		$rectifierIsDown: AlertEvent(
			$t0: _time,
			$neName: neName, 
			$rectifierServerSerial: serverSerial,
			$summary: summary,
			$circuit: circuit == "RECT - power",
			severity == 4,
			ncFunction == "INSERT")
		$powerIsBackUp: AlertEvent(
			$t1: _time,
			neName == $neName, 
			$acServerSerial: serverSerial,
			circuit == "AC - power",
			ncFunction == "DELETE",
			this after $rectifierIsDown)
		not( AlertEvent(
			serverSerial == $rectifierServerSerial,
			circuit == $circuit,
			ncFunction == "DELETE",
			this after [0s, 5s] $powerIsBackUp) )
		not( Issue( serverSerial == $rectifierServerSerial ) from issues.list)
	then
	    issues.create( new Issue( issues.newIssueId(), $rectifierServerSerial, $neName, $summary, $t0, $acServerSerial, $t1 ) );
		System.out.println("@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@");
		System.out.println("@");
		System.out.println("@ Major Alert at "+$neName);
		System.out.println("@");
		System.out.println("@ Issue "+issues.getLastIssueId()+" Created");
		System.out.println("@");
		System.out.println("@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@");
	    retract( $rectifierIsDown );
	    retract( $powerIsBackUp );
end