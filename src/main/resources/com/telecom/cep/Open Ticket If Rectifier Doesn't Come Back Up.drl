package com.telecom.open_ticket;

import com.telecom.cep.AlertEvent;

import java.util.Date;

declare AlertEvent
	@role(event)
	@timestamp(_time)
end

global Long startTime;
global Long startMemory;
global Long totalFactCount;
global java.util.HashMap issuesMap; 

rule "Open Ticket If Rectifier Doesn't Come Back Up After 5 Minutes"
dialect "mvel"
no-loop
	when
		$rectifierIsDown: AlertEvent(
			$t0: _time,
			$neName: neName, 
			$rectifierServerSerial: serverSerial,
			$summary: summary,
			$circuit: circuit == "RECT - power",
			severity == 4,
			ncFunction == "INSERT")
		$powerIsBackUp: AlertEvent(
			$t1: _time,
			neName == $neName, 
			$acServerSerial: serverSerial,
			circuit == "AC - power",
			ncFunction == "DELETE",
			this after $rectifierIsDown)
		not( AlertEvent(
			serverSerial == $rectifierServerSerial,
			circuit == $circuit,
			ncFunction == "DELETE",
			this after [0s, 5s] $powerIsBackUp) )
	then
		System.out.println("@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@");
		System.out.println("@");
		System.out.println("@ Major alert at "+$neName);
		System.out.println("@ Summary: "+$summary);
		System.out.println("@ Rectifier with server serial "+$rectifierServerSerial);
		System.out.println("@ went down at "+(new Date($t0)));
		System.out.println("@ AC Power with server serial "+$acServerSerial);
		System.out.println("@ went back up at "+(new Date($t1)));
		System.out.println("@ Rectifier did not come back up after 10m");
		System.out.println("@ Opened ticket # SNN000000088773 at "+(new Date()));			
		System.out.println("@");
		System.out.println("@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@");
end